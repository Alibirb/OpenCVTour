/*
 *    Copyright (C) 2015 Haruki Hasegawa
 *
 *    Licensed under the Apache License, Version 2.0 (the "License");
 *    you may not use this file except in compliance with the License.
 *    You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 *    Unless required by applicable law or agreed to in writing, software
 *    distributed under the License is distributed on an "AS IS" BASIS,
 *    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *    See the License for the specific language governing permissions and
 *    limitations under the License.
 */

def loadSigningConfigFromPropertiesFile(project, signingConfig, propFileName) {
    def props = new Properties()
    def propFile = project.file(propFileName)

    if (!propFile.exists()) {
        return false
    }

    props.load(propFile.newDataInputStream())

    if ((props.storeFile != null) &&
        (props.storePassword != null) &&
        (props.keyAlias != null) &&
        (props.keyPassword != null)) {

        signingConfig.storeFile = project.file(props.storeFile)
        signingConfig.storePassword = props.storePassword
        signingConfig.keyAlias = props.keyAlias
        signingConfig.keyPassword = props.keyPassword

        return true
    } else {
        return false
    }
}

// Export methods by turning them into closures
ext {
    loadSigningConfigFromPropertiesFile = this.&loadSigningConfigFromPropertiesFile
}
